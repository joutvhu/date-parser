plugins {
    id 'java'
    id 'idea'
    id 'groovy'
    id 'jacoco'
    id 'signing'
    id 'maven-publish'
}

group 'com.github.joutvhu'
version '1.0.0.3-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def snapshotVersion = version.endsWith('-SNAPSHOT') || version.endsWith('.SNAPSHOT')

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    compileOnly 'org.projectlombok:lombok:1.18.20'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'

    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
}

jar {
    manifest {
        attributes 'Built-By': 'joutvhu (Giao Ho)'
        attributes 'Build-Date': java.time.Instant.now().toString()
        attributes 'Bundle-Name': 'Date Parser'
        attributes 'Bundle-Vendor': project.group
        attributes 'Bundle-SymbolicName': project.name
        attributes 'Bundle-Version': project.version
    }
    into("META-INF/maven/${project.group}/${project.name}") {
        from {
            generatePomFileForMavenPublication
        }
        rename {
            it.replace('pom-default.xml', 'pom.xml')
        }
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives fatJar, sourcesJar, javadocJar
}

tasks.withType(Test) {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
    }
    filter {
        includeTestsMatching 'com.joutvhu.date.parser.*'
    }
}

task codeCoverageReport(type: JacocoReport, dependsOn: ['test']) {
    executionData test
    sourceSets sourceSets.main

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = project.name
            artifacts = [fatJar, sourcesJar, javadocJar]
            version = version

            pom {
                name = project.name
                description = 'Utility to parse String to Date by target type and string format'
                url = 'https://github.com/joutvhu/date-parser'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/joutvhu/date-parser/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'joutvhu'
                        name = 'Giao Ho'
                        email = 'joutvhu@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:joutvhu/date-parser.git'
                    developerConnection = 'scm:git:git@github.com:joutvhu/date-parser.git'
                    url = 'https://github.com/joutvhu/date-parser'
                }
                issueManagement {
                    system = 'Github Issue'
                    url = 'https://github.com/joutvhu/date-parser/issues'
                }
                organization {
                    name = 'Giao Ho'
                    url = 'https://github.com/joutvhu'
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'sonatype'
            if (snapshotVersion) {
                url = 'https://oss.sonatype.org/content/repositories/snapshots'
            } else {
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            }
            if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                credentials {
                    username = project.ossrhUsername
                    password = project.ossrhPassword
                }
            }
        }
        maven {
            name = 'github'
            url = 'https://maven.pkg.github.com/joutvhu/date-parser'
            if (project.hasProperty('githubUsername') && project.hasProperty('githubPassword')) {
                credentials {
                    username = project.githubUsername
                    password = project.githubPassword
                }
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

tasks.publishMavenPublicationToGithubRepository.configure {
    onlyIf { !snapshotVersion }
}
